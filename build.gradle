import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "4.0.3"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    implementation 'io.dropwizard:dropwizard-core:1.3.5'

    testImplementation platform(group: 'org.junit', name: 'junit-bom', version: '5.3.1')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine'
}

test {
    systemProperty 'samuel.ciphers.allow_restricted', true
    useJUnitPlatform {
        includeEngines 'junit-jupiter', 'junit-vintage'
    }

    failFast = true
}

// Define the main class for the application
mainClassName = 'com.adamkunicki.dropwizard.helloworld.HelloWorldApplication'

run {
    args = ['server', 'config.yaml']
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
            .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveName "${rootProject.name}.jar"
}
